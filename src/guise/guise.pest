WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

number = @{
    "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

color = @{ "#" ~ ASCII_HEX_DIGIT{3, 8} }

unit = @{ "px" | "vw" | "vh" | "vmin" | "vmax" | "%" }

length = ${ number ~ unit }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "\"" ~ #text = inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

asset = { "$(" ~ string ~ ")" }

prop_key = _{ identifier | string }

key_value = { prop_key ~ ":" ~ expression }

object_type = _{ qualified_name | string }
object_body =  { (key_value ~ ("," ~ key_value)* ~ ","?) }
object      =  { object_type ~ "{" ~ object_body? ~ "}" }

array = { "[" ~ (expression ~ ("," ~ expression)* ~ ","?)? ~ "]" }

expression = _{
    length
  | number
  | boolean
  | null
  | color
  | object
  | array
  | identifier
  | string
  | asset
}

arg_list = { "(" ~ ")" }

group = { "{" ~ (decl ~ ("," ~ decl)* ~ ","?)? ~ "}" }

decl = { prop_key ~ arg_list? ~ ":" ~ (group | object) }

qualified_name = @{ identifier ~ ("::" ~ identifier)* }
use_decl       =  { "use" ~ qualified_name ~ ";" }

guise = _{ SOI ~ (use_decl | decl)* ~ EOI }
